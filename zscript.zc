Version "4.8.0"

class ExtraLivesEventHandler : StaticEventHandler
{
	const RES_DELAY = 200;
	const SCALE = 3;
	const SPACING = 0;
	const SCALE_X = .02;
	const SCALE_Y = .83;

	protected CVar default_extra_lives;
	protected CVar extra_lives;
	protected int tick;
	protected PlayerInfo p;
	protected int reset;

	//The player has died; send them a message depending on whether they have lives or not.
	override void PlayerDied(PlayerEvent e)
	{
		if( getLives() == 1 )
		{
			Console.Printf(TEXTCOLOR_YELLOW .. "I am done helping you, "..p.GetUserName());
		}
		else if(hasLivesLeft())
		{
			Console.Printf(TEXTCOLOR_GREEN .. "You are lucky that I am merciful, "..p.GetUserName());
		}
		else
		{
			Console.Printf(TEXTCOLOR_RED .. "You are out of chances, "..p.GetUserName());
			Console.Printf(TEXTCOLOR_RED .. "Press 'use' to continue if you are a wimp");
		}
	}

	//Mod Initialisation
	override void PlayerEntered(PlayerEvent e)
	{
		p = players[e.PlayerNumber];
		extra_lives = CVar.GetCVar("extra_lives",p);
		default_extra_lives = CVar.GetCVar("default_extra_lives",p);
		tick = 0;

		if( reset )
		{
			setLives(getDefaultLives());
			reset = false;
		}
	}

	//New Game flag; no PlayerInfo yet, so we delay until the player exists
	override void NewGame()
	{
		reset = true;
	}

	override void PlayerSpawned(PlayerEvent e)
	{
		if(!Level.IsCrouchingAllowed() && !Level.IsJumpingAllowed())
		{
			Console.Printf(TEXTCOLOR_RED .. "Crouching and Jumping are not allowed here.\n");
		}
		else if(!Level.IsCrouchingAllowed())
		{
			Console.Printf(TEXTCOLOR_RED .. "Crouching is not allowed here.\n");
		}
		else if(!Level.IsJumpingAllowed())
		{
			Console.Printf(TEXTCOLOR_RED .. "Jumping is not allowed here.\n");
		}
	}

	override void PlayerRespawned(PlayerEvent e)
	{
		if( getLives() <= 0 )
		{
			Console.Printf(TEXTCOLOR_RED .. "Did you really think I'd allow that, %s?\n", getName());
			die();
			return;
		}

		reduceLives();
	}

	//Processes the resurrect delay so it doesn't happen immediately
	//This should only not immediately return when the player is dead
	override void WorldTick()
	{
		if( p.playerstate != PST_DEAD || !hasLivesLeft() )
		{
			tick = 0;
			return;
		}

		tick++;

		if( tick >= RES_DELAY )
		{
			resurrect();
		}
	}

	//Renders the HUD overlay; note that it uses multiplicative scaling
	//Therefore, it will not render in the same pixel-perfect position
	//On all resolutions
	override void renderOverlay(RenderEvent event)
	{
		int lives = CVar.GetCVar("extra_lives",p).GetInt();
		int livesColour = Font.CR_GREEN;

		if( lives == 0 )
		{
			livesColour = Font.CR_RED;
		}
		else if( lives == 1 )
		{
			livesColour = Font.CR_YELLOW;
		}

		Screen.DrawText(bigfont, livesColour, Screen.GetWidth()*(SCALE_X), Screen.GetHeight()*(SCALE_Y), "Lives: "..extra_lives.GetInt(), DTA_Spacing, SPACING, DTA_ScaleX, SCALE, DTA_ScaleY, SCALE);
	}

	//Prevent pressing 'use' while dead
	override bool InputProcess(InputEvent e)
	{
		if( p.playerstate == PST_DEAD && Bindings.GetBinding(e.keyScan) == "+use" )
		{
			Console.Printf(TEXTCOLOR_RED .. "Did you really thing it'd be that easy, %s?\n", p.GetUserName());
			return true;
		}

		return false;
	}

	//Terminate the player with extreme prejudice ;)
	void die()
	{
		p.health = 0;
		p.playerstate = PST_DEAD;
		setLives(0);
	}

	//Get default number of lives
	int getDefaultLives()
	{
		return default_extra_lives.GetInt();
	}

	//Get lives remaining
	int getLives()
	{
		if( extra_lives.GetInt() < 0 )
		{
			extra_lives.SetInt(getDefaultLives());
		}

		return extra_lives.GetInt();
	}

	//Set lives to a specific value
	int setLives(int lives)
	{
		if( lives < 0 )
		{
			lives = 0;
		}

		extra_lives.SetInt(lives);
		return getLives();
	}

	//Reduce lives by 1
	int reduceLives()
	{
		return setLives(getLives()-1);
	}

	//Does the player have lives left?
	bool hasLivesLeft()
	{
		return getLives() > 0;
	}

	//Resurrect the player
	void resurrect()
	{
		p.Resurrect();
		tick = 0;
		Console.Printf(TEXTCOLOR_ORANGE .. "Do not disappoint me this time, %s\n", getName());
	}

	//Returns the player's name
	string getName()
	{
		return p.GetUserName();
	}

	//Get the correct version of the word "lives"
	string livesWord()
	{
		if( getLives() == 1 )
		{
			return "life";
		}
		else
		{
			return "lives";
		}
	}
}